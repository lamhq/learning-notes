# Serverless

## Creating Lambda layer

This guide:
- Install project dependencies to a directory
- Package project dependencies into a zip file
- Create a lambda layer with the zip file

```hcl filename="main.tf"
# Configure the AWS Provider
provider "aws" {
  region = "us-east-1"
}
```

```hcl filename="variables.tf"
# define path for the build directory
variable "build_dir" {
  type        = string
  description = "Path to the build directory"
  default     = "../build"
}
```

```hcl filename="layer.tf"
# install project dependencies
resource "terraform_data" "install_dependencies" {
  provisioner "local-exec" {
    command = "./install-deps.sh"
  }
  triggers_replace = [
    !fileexists("${var.build_dir}/requirements.txt")
  ]
}

# packaging dependencies to zip file
# equivalent to `cd ../build/layer && zip -r ../layer.zip python`
data "archive_file" "layer_zip" {
  type        = "zip"
  source_dir  = "${var.build_dir}/layer"
  output_path = "${var.build_dir}/layer.zip"
  # wait for dependencies to be installed before packaging it
  depends_on = [terraform_data.install_dependencies]
}

# create a lambda layer that contains all project dependencies
resource "aws_lambda_layer_version" "lambda_layer" {
  filename   = "${var.build_dir}/layer.zip"
  # source_code_hash = filebase64sha256("../build/layer.zip")
  source_code_hash = data.archive_file.layer_zip.output_base64sha256
  layer_name = "dependencies"
  compatible_runtimes = ["python3.9"]
}
```


## Creating lambda function

- Package function code into a zip file
- Create a Lambda function with a layer (declared in other place)

```hcl filename="function.tf"
# packaging function code to zip file
data "archive_file" "function_zip" {
  type        = "zip"
  source_dir  = "../"
  excludes    = [".venv", "build", "infra"]
  output_path = "${var.build_dir}/function.zip"
}

# create a lambda function
resource "aws_lambda_function" "test_lambda_function" {
  function_name    = "lambdaTest"
  filename         = "${var.build_dir}/function.zip"
  source_code_hash = data.archive_file.function_zip.output_base64sha256
  role             = aws_iam_role.lambda_role.arn
  runtime          = "python3.9"
  handler          = "app.function.lambda_handler"
  timeout          = 10
  layers           = [aws_lambda_layer_version.lambda_layer.arn]
}
```
